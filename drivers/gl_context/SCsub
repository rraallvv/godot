Import('env')
Import('env_drivers')

env_glew = env_drivers.Clone()

if (env["platform"] in ["haiku","osx","windows","x11"]):
	# Thirdparty source files
	if (env["glew"] != "system"): # builtin
		thirdparty_dir = "#thirdparty/glew/"
		thirdparty_sources = [
			"glew.c",
		]
		thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

		env_glew.add_source_files(env.drivers_sources, thirdparty_sources)
		env_glew.Append(CPPFLAGS = ['-DGLEW_STATIC'])
		env_glew.Append(CPPPATH = [thirdparty_dir])
		# Also pass to global env, used by platform code
		env.Append(CPPPATH = [thirdparty_dir])

	env.Append(CPPFLAGS = ['-DGLEW_ENABLED'])

# Godot source files
env_glew.add_source_files(env.drivers_sources, "*.cpp")

Export('env')
